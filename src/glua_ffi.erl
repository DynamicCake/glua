-module(glua_ffi).

-import(luerl_lib, [lua_error/2]).

-export([coerce/1, coerce_nil/0, coerce_userdata/1, wrap_fun/2, sandbox_fun/1,
         get_table_keys/2, get_private/2, set_table_keys/3, load/2, load_file/2, eval/2, eval_file/2,
         eval_chunk/2, call_function/3, call_enc_function/3, is_lua_func/1, alloc/2, transform/2]).

% Precondition: [[See glua.gleam]]
transform(Term, {transformation, false, false}) ->
    Term;
transform(Term, Settings = {transformation, Proplist, _Funcs}) when is_list(Term) ->
    Result = [{K, transform(V, Settings)} || {K, V} <- Term],
    case Proplist of
        true -> maps:from_list(Result);
        false -> Result
    end;
transform(Func, {transformation, _Proplist, true}) when is_function(Func, 2) ->
    {lua_func, Func};
transform(Term, {transformation, _Proplist, _Funcs}) ->
    Term.

%% turn `{userdata, Data}` into `Data` to make it more easy to decode it in Gleam
maybe_process_userdata(Lst) when is_list(Lst) ->
    lists:map(fun maybe_process_userdata/1, Lst);
maybe_process_userdata({userdata, Data}) ->
    Data;
maybe_process_userdata(Other) ->
    Other.

%% helper to convert luerl return values to a format
%% that is more suitable for use in Gleam code
to_gleam(Value) ->
    case Value of
        {ok, Result, LuaState} ->
            Values = maybe_process_userdata(Result),
            {ok, {LuaState, Values}};
        {ok, _} = Result ->
            maybe_process_userdata(Result);
        {lua_error, _, _} = Error ->
            {error, map_error(Error)};
        {error, _, _} = Error ->
            {error, map_error(Error)};
        error ->
            {error, unknown_error}
    end.

%% helper to determine if a value is encoded or not
%% borrowed from https://github.com/tv-labs/lua/blob/main/lib/lua/util.ex#L19-L35
is_encoded(nil) ->
    true;
is_encoded(true) ->
    true;
is_encoded(false) ->
    true;
is_encoded(Binary) when is_binary(Binary) ->
    true;
is_encoded(N) when is_number(N) ->
    true;
is_encoded({tref,_}) ->
    true;
is_encoded({usrdef,_}) ->
    true;
is_encoded({eref,_}) ->
    true;
is_encoded({funref,_,_}) ->
    true;
is_encoded({erl_func,_}) ->
    true;
is_encoded({erl_mfa,_,_,_}) ->
    true;
is_encoded(_) ->
    false.

encode(X, St0) ->
    case is_encoded(X) of
        true -> {X, St0};
        false -> luerl:encode(X, St0)
    end.

encode_list(L, St0) when is_list(L) ->
    Enc = fun(X, {L1, St}) ->
                  {Enc, St1} = encode(X, St),
                  {[Enc | L1], St1}
          end,
    {L1, St1} = lists:foldl(Enc, {[], St0}, L),
    {lists:reverse(L1), St1}.


%% TODO: Improve compiler errors handling and try to detect more errors
map_error({error, [{_, luerl_parse, Errors} | _], _}) ->
    FormattedErrors = lists:map(fun(E) -> list_to_binary(E) end, Errors),
    {lua_compiler_exception, FormattedErrors};
map_error({lua_error, {illegal_index, Tbl, Value}, State}) ->
    FormattedTbl = list_to_binary(io_lib:format("~p", [Tbl])),
    FormattedValue = unicode:characters_to_binary(Value),
    {lua_runtime_exception, {illegal_index, FormattedTbl, FormattedValue}, State};
map_error({lua_error, {error_call, _} = Error, State}) ->
    {lua_runtime_exception, Error, State};
map_error({lua_error, {undefined_function, Value}, State}) ->
    {lua_runtime_exception,
     {undefined_function, list_to_binary(io_lib:format("~p", [Value]))},
     State};
map_error({lua_error, {badarith, Operator, Args}, State}) ->
    FormattedOperator = unicode:characters_to_binary(atom_to_list(Operator)),
    FormattedArgs =
        lists:map(fun(V) ->
                     unicode:characters_to_binary(
                         io_lib:format("~p", [V]))
                  end,
                  Args),
    {lua_runtime_exception, {bad_arith, FormattedOperator, FormattedArgs}, State};
map_error({lua_error, {assert_error, _} = Error, State}) ->
    {lua_runtime_exception, Error, State};
map_error({lua_error, _, State}) ->
    {lua_runtime_exception, unknown_exception, State};
map_error(_) ->
    unknown_error.

coerce(X) ->
    X.

coerce_nil() ->
    nil.

coerce_userdata(X) ->
    {userdata, X}.

alloc(St0, Value) when is_list(Value) ->
    {Enc, St1} = luerl_heap:alloc_table(Value, St0),
    {St1, Enc};
alloc(St0, {usrdef,_}=Value) ->
    {Enc, St1} = luerl_heap:alloc_userdata(Value, St0),
    {St1, Enc};
alloc(St0, Other) ->
    {St0, Other}.

wrap_fun(Fun, Settings) ->
    fun(Args, State) ->
            Decoded = luerl:decode_list(Args, State),
            Transformed = [transform(Arg, Settings) || Arg <- Decoded],
            {NewState, Ret} = Fun(State, Transformed),
            luerl:encode_list(Ret, NewState)
    end.

sandbox_fun(Msg) ->
    fun(_, State) -> {error, map_error(lua_error({error_call, [Msg]}, State))} end.

get_table_keys(Lua, Keys) ->
    case luerl:get_table_keys(Keys, Lua) of
        {ok, nil, _} ->
            {error, key_not_found};
        {ok, Value, _} ->
            {ok, Value};
        Other ->
            to_gleam(Other)
    end.

set_table_keys(Lua, Keys, Value) ->
    SetFun = case is_encoded(Value) of
                 true -> fun luerl:set_table_keys/3;
                 false -> fun luerl:set_table_keys_dec/3
             end,
    to_gleam(SetFun(Keys, Value, Lua)).

load(Lua, Code) ->
    to_gleam(luerl:load(
                 unicode:characters_to_list(Code), Lua)).

load_file(Lua, Path) ->
    to_gleam(luerl:loadfile(
                 unicode:characters_to_list(Path), Lua)).

eval(Lua, Code) ->
    to_gleam(luerl:do(
                 unicode:characters_to_list(Code), Lua)).

eval_chunk(Lua, Chunk) ->
    to_gleam(luerl:call_chunk(Chunk, Lua)).

eval_file(Lua, Path) ->
    to_gleam(luerl:dofile(
                 unicode:characters_to_list(Path), Lua)).

is_lua_func({lua_func, Func}) when is_function(Func, 2) ->
    true;
is_lua_func(_Other) ->
    false.

call_function(Lua, Fun, Args) ->
    {EncodedArgs, State} = encode_list(Args, Lua),
    to_gleam(luerl:call(Fun, EncodedArgs, State)).

call_enc_function(Lua, {lua_func, Func}, Args) ->
    Func(Args).

get_private(Lua, Key) ->
    try
        {ok, luerl:get_private(Key, Lua)}
    catch
        error:{badkey, _} ->
            {error, key_not_found}
    end.
