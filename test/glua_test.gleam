import gleam/dict
import gleam/dynamic
import gleam/dynamic/decode
import gleam/int
import gleam/list
import gleam/option
import gleam/pair
import gleam/result
import gleeunit
import glua

pub fn main() -> Nil {
  gleeunit.main()
}

pub fn get_table_test() {
  let lua = glua.new()
  let my_table = [
    #("meaning of life", 42),
    #("pi", 3),
    #("euler's number", 3),
  ]
  let cool_numbers =
    glua.function(fn(lua, _params) {
      let table =
        glua.table(
          my_table
          |> list.map(fn(pair) { #(glua.string(pair.0), glua.int(pair.1)) }),
        )
      #(lua, [table])
    })

  let assert Ok(lua) = glua.set(lua, ["cool_numbers"], cool_numbers)
  let assert Ok(#(_lua, [table])) =
    glua.call_function_by_name(
      lua,
      ["cool_numbers"],
      [],
      using: glua.table_decoder(decode.string, decode.int),
    )

  assert dict.from_list(table) == dict.from_list(my_table)
}

pub fn sandbox_test() {
  let assert Ok(lua) = glua.sandbox(glua.new(), ["math", "max"])
  let args = list.map([20, 10], glua.int)

  let assert Error(glua.LuaRuntimeException(exception, _)) =
    glua.call_function_by_name(
      state: lua,
      keys: ["math", "max"],
      args:,
      using: decode.int,
    )

  assert exception == glua.ErrorCall(["math.max is sandboxed"])

  let assert Ok(lua) = glua.sandbox(glua.new(), ["string"])

  let assert Error(glua.LuaRuntimeException(glua.IllegalIndex(_, name), _)) =
    glua.eval(state: lua, code: "return string.upper('my_string')")
    |> glua.dec_one(decode.string)

  assert name == "upper"

  let assert Ok(lua) = glua.sandbox(glua.new(), ["os", "execute"])

  let assert Error(glua.LuaRuntimeException(exception, _)) =
    glua.eval(
      state: lua,
      code: "os.execute(\"echo 'sandbox test is failing'\"); os.exit(1)",
    )

  assert exception == glua.ErrorCall(["os.execute is sandboxed"])

  let assert Ok(lua) = glua.sandbox(glua.new(), ["print"])
  let arg = glua.string("sandbox test is failing")
  let assert Error(glua.LuaRuntimeException(exception, _)) =
    glua.call_function_by_name(
      state: lua,
      keys: ["print"],
      args: [arg],
      using: decode.string,
    )

  assert exception == glua.ErrorCall(["print is sandboxed"])
}

pub fn new_sandboxed_test() {
  let assert Ok(lua) = glua.new_sandboxed([])

  let assert Error(glua.LuaRuntimeException(exception, _)) =
    glua.eval(state: lua, code: "return load(\"return 1\")")

  assert exception == glua.ErrorCall(["load is sandboxed"])

  let arg = glua.int(1)
  let assert Error(glua.LuaRuntimeException(exception, _)) =
    glua.ref_call_function_by_name(state: lua, keys: ["os", "exit"], args: [arg])

  assert exception == glua.ErrorCall(["os.exit is sandboxed"])

  let assert Error(glua.LuaRuntimeException(glua.IllegalIndex(_, name), _)) =
    glua.eval(state: lua, code: "io.write('some_message')")

  assert name == "write"

  let assert Ok(lua) = glua.new_sandboxed([["package"], ["require"]])
  let assert Ok(lua) = glua.set_lua_paths(lua, paths: ["./test/lua/?.lua"])

  let code = "local s = require 'example'; return s"
  let assert Ok(#(_, result)) =
    glua.eval(state: lua, code:)
    |> glua.dec_one(using: decode.string)

  assert result == "LUA IS AN EMBEDDABLE LANGUAGE"
}

pub fn encoding_and_decoding_nested_tables_test() {
  let nested_table = [
    #(
      glua.string("key"),
      glua.table([
        #(
          glua.int(1),
          glua.table([#(glua.string("deeper_key"), glua.string("deeper_value"))]),
        ),
      ]),
    ),
  ]

  let keys = ["my_nested_table"]

  let nested_table_decoder =
    glua.table_decoder(
      decode.string,
      glua.table_decoder(
        decode.int,
        glua.table_decoder(decode.string, decode.string),
      ),
    )
  let tbl = glua.table(nested_table)

  let assert Ok(lua) = glua.set(state: glua.new(), keys:, value: tbl)

  let assert Ok(result) =
    glua.get(state: lua, keys:)
    |> glua.gdec(nested_table_decoder)

  assert result == [#("key", [#(1, [#("deeper_key", "deeper_value")])])]
}

pub type TestUserdata {
  TestUserdata(foo: String, bar: Int)
}

pub fn userdata_test() {
  let lua = glua.new()
  let userdata = TestUserdata("my-userdata", 1)
  // TODO: Fix so it isn't so weird
  let userdata_decoder = {
    use data <- decode.field(1, {
      use foo <- decode.field(1, decode.string)
      use bar <- decode.field(2, decode.int)
      decode.success(TestUserdata(foo:, bar:))
    })
    decode.success(data)
  }

  let assert Ok(lua) = glua.set(lua, ["my_userdata"], glua.userdata(userdata))
  let assert Ok(#(lua, result)) =
    glua.eval(lua, "return my_userdata")
    |> glua.dec_one(userdata_decoder)

  assert result == userdata

  let userdata = TestUserdata("other_userdata", 2)
  let assert Ok(lua) =
    glua.set(lua, ["my_other_userdata"], glua.userdata(userdata))
  let assert Error(glua.LuaRuntimeException(glua.IllegalIndex(value, index), _)) =
    glua.eval(lua, "return my_other_userdata.foo")
    |> glua.dec_one(decode.string)

  assert value == "{usdref,1}"
  assert index == "foo"
}

pub fn get_test() {
  let state = glua.new()

  let assert Ok(pi) =
    glua.get(state: state, keys: ["math", "pi"])
    |> glua.gdec(using: decode.float)

  assert pi >. 3.14 && pi <. 3.15

  let keys = ["my_table", "my_value"]
  let encoded = glua.bool(True)
  let assert Ok(state) = glua.set(state:, keys:, value: encoded)
  let assert Ok(ret) =
    glua.get(state:, keys:)
    |> glua.gdec(using: decode.bool)

  assert ret == True

  let code =
    "
  my_value = 10
  return 'ignored'
"
  let assert Ok(#(state, _)) =
    glua.new()
    |> glua.eval(code:)
    |> glua.dec_one(using: decode.string)
  let assert Ok(ret) =
    glua.get(state:, keys: ["my_value"])
    |> glua.gdec(using: decode.int)

  assert ret == 10
}

pub fn get_returns_proper_errors_test() {
  let state = glua.new()

  assert glua.get(state:, keys: ["non_existent_global"])
    |> glua.gdec(using: decode.string)
    == Error(glua.KeyNotFound)

  let encoded = glua.int(10)
  let assert Ok(state) =
    glua.set(state:, keys: ["my_table", "some_value"], value: encoded)

  assert glua.get(state:, keys: ["my_table", "my_val"])
    |> glua.gdec(using: decode.int)
    == Error(glua.KeyNotFound)
}

pub fn set_test() {
  let encoded = glua.string("custom version")

  let assert Ok(lua) =
    glua.set(state: glua.new(), keys: ["_VERSION"], value: encoded)
  let assert Ok(result) =
    glua.get(state: lua, keys: ["_VERSION"])
    |> glua.gdec(using: decode.string)

  assert result == "custom version"

  let numbers =
    [2, 4, 7, 12]
    |> list.index_map(fn(n, i) { #(i + 1, n * n) })

  let keys = ["math", "squares"]

  let encoded =
    glua.table(
      numbers |> list.map(fn(pair) { #(glua.int(pair.0), glua.int(pair.1)) }),
    )
  let assert Ok(lua) = glua.set(lua, keys, encoded)

  assert glua.get(lua, keys)
    |> glua.gdec(glua.table_decoder(decode.int, decode.int))
    == Ok([#(1, 4), #(2, 16), #(3, 49), #(4, 144)])

  let count_odd = fn(lua: glua.Lua, args: List(dynamic.Dynamic)) {
    let assert [list] = args
    let assert Ok(list) =
      decode.run(list, glua.table_decoder(decode.int, decode.int))

    let count =
      list.map(list, pair.second)
      |> list.count(int.is_odd)

    #(lua, list.map([count], glua.int))
  }

  let encoded = glua.function(count_odd)
  let assert Ok(lua) = glua.set(glua.new(), ["count_odd"], encoded)

  let arg =
    glua.table(
      list.index_map(list.range(1, 10), fn(i, n) {
        #(glua.int(i + 1), glua.int(n))
      }),
    )

  let assert Ok(#(lua, [result])) =
    glua.call_function_by_name(
      state: lua,
      keys: ["count_odd"],
      args: [arg],
      using: decode.int,
    )

  assert result == 5

  let tbl =
    glua.table([
      #(
        glua.string("is_even"),
        glua.function(fn(lua, args) {
          let assert [arg] = args
          let assert Ok(arg) = decode.run(arg, decode.int)
          #(lua, list.map([int.is_even(arg)], glua.bool))
        }),
      ),
      #(
        glua.string("is_odd"),
        glua.function(fn(lua, args) {
          let assert [arg] = args
          let assert Ok(arg) = decode.run(arg, decode.int)
          #(lua, list.map([int.is_odd(arg)], glua.bool))
        }),
      ),
    ])

  let arg = glua.int(4)

  let assert Ok(lua) = glua.set(state: lua, keys: ["my_functions"], value: tbl)

  let assert Ok(#(lua, [result])) =
    glua.call_function_by_name(
      state: lua,
      keys: ["my_functions", "is_even"],
      args: [arg],
      using: decode.bool,
    )

  assert result == True

  let assert Ok(#(_, result)) =
    glua.eval(state: lua, code: "return my_functions.is_odd(4)")
    |> glua.dec_one(using: decode.bool)

  assert result == False
}

pub fn set_lua_paths_test() {
  let assert Ok(state) =
    glua.set_lua_paths(state: glua.new(), paths: ["./test/lua/?.lua"])

  let code = "local s = require 'example'; return s"

  let assert Ok(#(_, result)) =
    glua.eval(state:, code:)
    |> glua.dec_one(using: decode.string)

  assert result == "LUA IS AN EMBEDDABLE LANGUAGE"
}

pub fn get_private_test() {
  assert glua.new()
    |> glua.set_private("test", [1, 2, 3])
    |> glua.get_private("test", using: decode.list(decode.int))
    == Ok([1, 2, 3])

  assert glua.new()
    |> glua.get_private("non_existent", using: decode.string)
    == Error(glua.KeyNotFound)
}

pub fn delete_private_test() {
  let lua = glua.set_private(glua.new(), "the_value", "that_will_be_deleted")

  assert glua.get_private(lua, "the_value", using: decode.string)
    == Ok("that_will_be_deleted")

  assert glua.delete_private(lua, "the_value")
    |> glua.get_private(key: "the_value", using: decode.string)
    == Error(glua.KeyNotFound)
}

pub fn load_test() {
  let assert Ok(#(lua, chunk)) =
    glua.load(state: glua.new(), code: "return 5 * 5")
  let assert Ok(#(_, result)) =
    glua.eval_chunk(state: lua, chunk:)
    |> glua.dec_one(using: decode.int)

  assert result == 25
}

pub fn eval_load_file_test() {
  let assert Ok(#(lua, chunk)) =
    glua.load_file(state: glua.new(), path: "./test/lua/example.lua")
  let assert Ok(#(_, result)) =
    glua.eval_chunk(state: lua, chunk:)
    |> glua.dec_one(using: decode.string)

  assert result == "LUA IS AN EMBEDDABLE LANGUAGE"
}

pub fn eval_test() {
  let assert Ok(#(lua, result)) =
    glua.eval(state: glua.new(), code: "return 'hello, ' .. 'world!'")
    |> glua.dec_one(using: decode.string)

  assert result == "hello, world!"

  let assert Ok(#(_, results)) =
    glua.eval(state: lua, code: "return 2 + 2, 3 - 1")
    |> glua.dec(using: decode.list(decode.int))

  assert results == [4, 2]
}

pub fn eval_returns_proper_errors_test() {
  let state = glua.new()

  assert glua.eval(state:, code: "if true then 1 + ")
    == Error(
      glua.LuaCompilerException(messages: ["syntax error before: ", "1"]),
    )

  assert glua.eval(state:, code: "return 'Hello from Lua!'")
    |> glua.dec_one(using: decode.int)
    == Error(
      glua.UnexpectedResultType([decode.DecodeError("Int", "String", ["0"])]),
    )

  let assert Error(glua.LuaRuntimeException(
    exception: glua.IllegalIndex(value:, index:),
    state: _,
  )) = glua.eval(state:, code: "return a.b")

  assert value == "nil"
  assert index == "b"

  let assert Error(glua.LuaRuntimeException(
    exception: glua.ErrorCall(messages:),
    state: _,
  )) =
    glua.eval(state:, code: "error('error message')")
    |> glua.dec_one(using: decode.int)

  assert messages == ["error message"]

  let assert Error(glua.LuaRuntimeException(
    exception: glua.UndefinedFunction(value:),
    state: _,
  )) = glua.eval(state:, code: "local a = 5; a()")

  assert value == "5"
  let assert Error(glua.LuaRuntimeException(
    exception: glua.BadArith(operator:, args:),
    state: _,
  )) = glua.eval(state:, code: "return 10 / 0")

  assert operator == "/"
  assert args == ["10", "0"]

  let assert Error(glua.LuaRuntimeException(
    exception: glua.AssertError(message:),
    state: _,
  )) = glua.eval(state:, code: "assert(1 == 2, 'assertion failed')")

  assert message == "assertion failed"
}

pub fn eval_file_test() {
  let assert Ok(#(_, [result])) =
    glua.eval_file(
      state: glua.new(),
      path: "./test/lua/example.lua",
      using: decode.string,
    )

  assert result == "LUA IS AN EMBEDDABLE LANGUAGE"
}

pub fn call_function_test() {
  let assert Ok(#(lua, [fun])) =
    glua.eval(state: glua.new(), code: "return string.reverse")
    |> result.map(glua.output_pair)

  let encoded = glua.string("auL")

  let assert Ok(#(lua, [result])) =
    glua.call_function(
      state: lua,
      ref: fun,
      args: [encoded],
      using: decode.string,
    )

  assert result == "Lua"

  let assert Ok(#(lua, [fun])) =
    glua.eval(state: lua, code: "return function(a, b) return a .. b end")
    |> result.map(glua.output_pair)

  let args = list.map(["Lua in ", "Gleam"], glua.string)

  let assert Ok(#(_, [result])) =
    glua.call_function(state: lua, ref: fun, args:, using: decode.string)

  assert result == "Lua in Gleam"
}

pub fn call_function_returns_proper_errors_test() {
  let state = glua.new()

  let assert Ok(#(state, [ref])) =
    glua.eval(state:, code: "return string.upper")
    |> result.map(glua.output_pair)

  let arg = glua.string("Hello from Gleam!")

  assert glua.call_function(state:, ref:, args: [arg], using: decode.int)
    == Error(
      glua.UnexpectedResultType([decode.DecodeError("Int", "String", [])]),
    )

  let assert Ok(#(lua, [ref])) =
    glua.eval(state:, code: "return 1")
    |> result.map(glua.output_pair)

  let assert Error(glua.LuaRuntimeException(
    exception: glua.UndefinedFunction(value:),
    state: _,
  )) = glua.call_function(state: lua, ref:, args: [], using: decode.string)

  assert value == "1"
}

pub fn call_function_by_name_test() {
  let args = list.map([20, 10], glua.int)
  let assert Ok(#(lua, [result])) =
    glua.call_function_by_name(
      state: glua.new(),
      keys: ["math", "max"],
      args:,
      using: decode.int,
    )

  assert result == 20

  let assert Ok(#(lua, [result])) =
    glua.call_function_by_name(
      state: lua,
      keys: ["math", "min"],
      args:,
      using: decode.int,
    )

  assert result == 10

  let arg = glua.float(10.2)
  let assert Ok(#(_, [result])) =
    glua.call_function_by_name(
      state: lua,
      keys: ["math", "type"],
      args: [arg],
      using: decode.optional(decode.string),
    )

  assert result == option.Some("float")
}

pub fn nested_function_references_test() {
  let code = "return function() return math.sqrt end"

  let assert Ok(#(lua, [ref])) =
    glua.eval(state: glua.new(), code:)
    |> result.map(glua.output_pair)
  let assert Ok(#(lua, [ref])) =
    glua.ref_call_function(state: lua, ref:, args: [])

  let arg = glua.int(400)
  let assert Ok(#(_, [result])) =
    glua.call_function(state: lua, ref:, args: [arg], using: decode.float)
  assert result == 20.0
}

pub fn alloc_test() {
  let #(lua, table) = glua.alloc_table(glua.new(), [])
  let proxy =
    glua.function(fn(lua, _args) { #(lua, [glua.string("constant")]) })
  let metatable = glua.table([#(glua.string("__index"), proxy)])
  let assert Ok(#(lua, _)) =
    glua.ref_call_function_by_name(lua, ["setmetatable"], [table, metatable])
  let assert Ok(lua) = glua.set(lua, ["test_table"], table)

  let assert Ok(#(_lua, ret1)) =
    glua.eval(lua, "return test_table.any_key")
    |> glua.dec_one(decode.string)

  let assert Ok(#(_lua, ret2)) =
    glua.eval(lua, "return test_table.other_key")
    |> glua.dec_one(decode.string)

  assert ret1 == "constant"
  assert ret2 == "constant"
}
